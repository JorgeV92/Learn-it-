(* 
                                C. Maximum Median
    
    You are given an array ð‘Ž of ð‘› integers, where ð‘› is odd. You can make the following operation with it:
    
    Choose one of the elements of the array (for example ð‘Žð‘– ) and increase it by 1 (that is, replace it with ð‘Žð‘–+1).
    You want to make the median of the array the largest possible using at most ð‘˜ operations.
    
    The median of the odd-sized array is the middle element after the array is sorted in non-decreasing order. 
    For example, the median of the array [1,5,2,3,5] is 3.
    
    Input
    The first line contains two integers ð‘› and ð‘˜ (1 â‰¤ ð‘› â‰¤ 2â‹…10^5, ð‘› is odd, 1 â‰¤ ð‘˜ â‰¤ 10^9) â€” 
    the number of elements in the array and the largest number of operations you can make.
    
    The second line contains ð‘› integers ð‘Ž1,ð‘Ž2,â€¦,ð‘Žð‘› (1 â‰¤ ð‘Žð‘– â‰¤ 10^9).
    
    Output
    Print a single integer â€” the maximum possible median after the operations.

    Example
    input 
    3 2
    1 3 5

    output
    5   
*)

open Printf

let () = 
  let read_int_array str =
    str 
      |> String.split_on_char ' '
      |> Array.of_list
      |> Array.map int_of_string
  in

  let num_line = input_line stdin in 
  let n, k = 
    match String.split_on_char ' ' num_line with 
      | [n1; n2] -> (int_of_string n1, int_of_string n2)
      | _ -> failwith "Expected exactly two numbers"
  in
  
  let a = read_int_array (input_line stdin) in 
  Array.sort compare a; 

  let good x = 
    let ops_needed = ref 0 in 
    for i = (n - 1) / 2 to n - 1 do 
      ops_needed := !ops_needed +  max 0 (x - a.(i));
    done;
    !ops_needed <= k
  in 
  
  let rec binary_search l r = 
    if l < r then 
      let m = l + (r - l + 1) / 2 in 
      if good m then 
        binary_search m r 
      else
        binary_search l (m - 1)
    else
      l 
  in
  let procedure = binary_search 0 (2 * 1_000_000_000) in 
  printf "%d\n" procedure


